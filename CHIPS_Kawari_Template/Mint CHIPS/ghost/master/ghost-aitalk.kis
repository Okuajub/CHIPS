#--
# AI Talk
#--
# The random talk file for your Ghost.

sentence : Hello.

sentence : "This is an example of how to escape double quotes in dialogue. \"I am I. I wish to be I.\", as he said in Episode 26. When you want to use quotes like this, just be mindful."
sentence : 'Because "double" quotes are less common in English, it makes sense to write in them. But maybe you prefer to write in \'single\' quotes. Don\'t think you can\'t escape them either.'

# SakuraScript
sentence : "\1\s[10]What's going on outside today?\0\s[0]Nothing much, I assume."
sentence : "\0\s[0]\1\s[10]Writing dialogue is very easy in Kawari! Don't you think?"

# Blocks
sentence : (
    "\0\s[0]SakuraScript can be used, even in quotes.\1\s[10]Kawari parses it easily.\w[500]\n"
    "But if you want to output a slash directly, you can use a triple slash, like so. \\\1\\\s[10]. Looks like an error, doesn't it!"
)

# Entries for random words
food : hard-boiled egg, yogurt chips, banana
food : sunflower seeds, cashews, rice cakes, puffed rice

dogFood : kibble, deer liver, Science Diet, Orijen Six Fish

ghosts, friends: Girl and Triangle, Snake and Otacon, Needle
ghosts, female : Emily, Mayura
ghosts, friends, male : Emilio

friends : Sakura, Cerberus, Emily

# Entry Calls
sentence : "\1\s[18]Hey, I'm hungry. Do you have any "${food}", Sentinel?\0\s[3]Sorry. I'm all out."
sentence : "\1\s[10]Are you friends with "${friends}"? I've heard they're pretty cool!"
sentence : "\1\s[16]Hey, have you tried out that Ghost, "${ghosts}"?\1\s[15] It's fun to try new Ghosts!"

# '&' Chooses a definition that is inside the 'ghosts' entry and is also inside the 'friends' entry.
sentence : "\0\s[0]This is an example of using the '&' operator in an entry call.\n\n[half]Chip, have you seen "${ghosts & friends}" lately?\1\s[10]Sorry, I can't say that I have."
# '+' Chooses a definition from either the 'ghosts' or 'friends' entry.
sentence : "\0\s[0]This is an example of using the '+' operator in an entry call.\n\n[half]Chip, have you seen "${ghosts + friends}" lately?\1\s[10]Sorry, I can't say that I have."
# '-' Chooses a definition that is in 'ghosts' entry but is not in 'friends' entry.
sentence : "\0\s[0]This is an example of using the '-' operator in an entry call.\n\n[half]Chip, have you seen "${ghosts - friends}" lately?\1\s[10]Sorry, I can't say that I have."
# You can use these with entries other than 'ghosts' and 'friends' too, don't get it mixed up! This would choose a definition that is either in 'food' or 'dogFood', too.
sentence : "\0\s[0]Hm.. My stomach is growling. Wonder where some of that "${food + dogFood}" is...\1\s[10]Hey-?"

# The following operation, '${ (ghosts + friends) & female}', asks for 'an entry that is from either the ghosts or friends entry' and is also in the 'female' entry.
sentence : "\0\s[0]"${ (ghosts + friends) & female}" is pretty cute, isn't she? All these teenagers and kids that are running around make me broody.\1\s[10]So you really do have a soft spot."
# You could do the same for a 'male' entry, too.
sentence : "\0\s[0]"${ (ghosts + friends) & male}" is pretty cute, isn't he? All these teenagers and kids that are running around make me broody.\1\s[10]So you really do have a soft spot."



num1 : 2, 4, 6, 8
num2 : 3, 5, 7, 9
num3 : 2
sentence : \0\s[0]The sum of ${num1} and ${num2} is $[ ${num1} + ${num2} ]. "\1\s[19]Are you sure that's right?\0\s[3]\n\n[half]\w2.\w2.\w2."
sentence : "\1\s[10]Unless the index number changes," $food[${num3}] "will always be 'banana'."


# History References
sentence : \0\s[8]The sum of ${num1} and ${num2} is $[ ${0} + ${1} ]. "\1\s[10]Now, that seems more correct. It's the same for "${-1} and ${0}"."

# Temporary Entries
sentence : $(pushstr @animals eels)"\1\s[12]My hovercraft is full of "${@animals}"."

# If/else if/else Statements
sentence : (
    $(if $[${food} == 'sunflower seeds'] "\1\s[18] I'm really craving some sunflower seeds, right now." 
    else if $[${dogFood} == 'deer liver'] "\0\s[8]Hm. Some deer liver would hit the spot."
    else "\0\s[0]Hungry?\1\s[10]\w4Not really.")
)

sentence : (
    $(pushstr @num $(rand 100))
    $(if $[${@num} > 90] "\0\s[5]The random number was greater than 90."
    else if $[${@num} > 50] "\0\s[3]The random number was greater than 50 but not 90."
    else "\0\s[3]The random number was not greater than 50."
    )
    " The random number was "${@num}"."
)

# Hour roll-over event. Not always precisely on the hour.
talk.OnHourTimeSignal : $(setstr @minute $(date %M)) "\0\s[0]It's %hour:"${@minute}"."

#--
# CHIPS Specific
#--
# User-centric Entries 
# username holds the ghost's name for the User. Uses tempname as default value, or "User" if tempname is invalid.
# userfull is their full name as described in the SSP preferences.
# tempname holds the user's nickname as set in SSP.
# usergender is the user's gender as set by the SSP preferences. "male", "female" and "undef" are the three values it can have.
# birthdate is the user's birthday in SSP's preferences. Formed as "yyyy,mm,dd", e.g. "1972,10,31" for "October 31st, 1972".

# Pronouns
sentence : (
    # 'username' calls the user's name and makes the user the subject.
    "\1\s[10] Hey, Sentinel. Do you think "${username}" should use pronouns to refer to the User?"
    # Here, the entry 'their' is called. It'll choose an appropriate possessive determiner (such as "their") for the subject.
    "\0\s[0]I think it's "${their}" choice. "
    # The function 'SP' is used to determine if a word should be singular or plural relative to the last pronoun. 
    "I've got no idea what "${they}$(SP "'s" "'ve")" got planned."
)

# Dev Mode
# Eventually, you'll probably want to test something, or create an option that only you, the developer, can see.
# 'devmode' is a special entry in CHIPS that is used to engage development mode. To engage it, enter '$(setstr devmode 1)' in the script input.
# This will set 'devmode' to be ON. Be aware, smart users can also do this, so don't put any sensitive information behind devmode. 
# It's better used to do things like hide debug menus, or neatly turn off a buggy function you're still working on.

# Example Dialogue
sentence : "\0\s[5]You can replace our lines with whatever you'd like."

sentence : "\1\s[19]Have you tried turning it off and on again?"

sentence : (
    "\0\s[8]Chip, have you heard of a person called 'Melinda the Mental Marvel'?\w8\w8"
    "\1\s[19]No, why?\w8"
    "\0\s[0]\n\n[half]Oh, nothing.. You just remind me of this kid I met once. Chip McCallahan."
    "\1\s[13]\n\n[half]Oh, \f[italic,true]him\f[italic,default]? No way! I \f[italic,true]test\f[italic,false] chips, not collect them to get into some lame club.\w8"
    "\0\s[0]\n\n[half]Bummer."
    "\1\s[19]\n\n[half]\f[size,9]Chip's Challenge... Huh!\f[size,default]"
)

# Lets set up some entries...
media : "'The End of Evangelion'", "'Pacific Rim'", "'Robin Hood'", "'Aliens'", "'Citizen Kane'", "'John Wick'", "'Johnny Mnemonic'", "'Metal Gear Solid 4: Guns of the Patriots'"
media : "'Uncharted'", "'Bram Stoker's Dracula'", "'Resident Evil'"

mediaCritiques : "I thought the pacing was too slow.", "The music was so bland. I don't get why it's praised all the time.", "It was totally just a jab at the fans.", "It looked so cheap!"
mediaCritiques : "Genuinely unpleasant to watch. Couldn't some of the effects budget gone to the writing instead?", "The main character didn't have any chemistry at all."

mediaDefense   : "Oh, come on, it wasn't \f[italic,true]that\f[italic,default] bad.", "How can you say that, when the subtext was so good?", "Clearly you just don't understand it."
mediaDefense   : "I think that the music really sells it. C'mon, I'll set up a home theater and you can see it! Or, hear it.", "Oh, that's not fair. It's totally subjective."

# Putting them all together, this one dialogue has a possibility of 360 different variations. Isn't that helpful? Even discounting the media entry call, that's still 60 different variations!
sentence : (
    "\1\s[18]So, Sentinel, what'd you think of "${media}"?\n\n[half]"
    "\0\s[8]Hmm. I liked it. But.. "${mediaCritiques}"\n\n[half]"
    "\1\s[13]"${mediaDefense}
    "\0\s[6]Suit yourself."
)

# Nonoverlap function. Don't write anything past this point.
=kis
Nonoverlap sentence;
=end